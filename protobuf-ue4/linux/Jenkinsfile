#!/usr/bin/env groovy

def gitRepo = 'https://github.com/kingsoftgames/protobuf-ue4.git'

ENGINE_LOCAL_DIR = 'UnrealEngine'
ENGINE_SYS = 'Linux'

UE4_TOOL_CHAIN_LIST = [
    [
        UE4_VERSION            : '4.21',
        UE4_CLANG_VERSION      : 'v12_clang-6.0.1-centos7',
        UE4_ZLIB_VERSION       : 'v1.2.8'
    ],
    [
        UE4_VERSION            : '4.25',
        UE4_CLANG_VERSION      : 'v16_clang-9.0.1-centos7',
        UE4_ZLIB_VERSION       : 'v1.2.8'
    ],
]

def getToolChain(UE4Version) {
    for (toolChain in UE4_TOOL_CHAIN_LIST) {
        if (UE4Version.startsWith("${toolChain.UE4_VERSION}.")) {
            return toolChain
        }
    }
    return null
}

pipeline {
    agent {
        label "ubuntu:18.04 && thirdparty"
    }
    options {
        skipDefaultCheckout()
    }
    parameters {
        string(name: 'PROTOBUF_UE4_VERSION', defaultValue: '3.9.2')
        string(name: 'MYCFLAGS', defaultValue: '')
        string(name: 'MYLDFLAGS', defaultValue: '')
        string(name: 'ENGINE_BRANCH', defaultValue: 'release')
        booleanParam(name: 'CLEAN_WS',
            defaultValue: true,
            description: 'When checked, call function cleanWs.')
    }
    environment {
        PROTOBUF_UE4_WORKSPACE           = "${env.WORKSPACE}"
        UE4_ROOT                         = "${env.WORKSPACE}/UnrealEngine"
        PROTOBUF_UE4_VERSION             = "${params.PROTOBUF_UE4_VERSION}"
        PROTOBUF_UE4_PREFIX              = "${env.HOME}/thirdparty/protobuf-ue4/${params.PROTOBUF_UE4_VERSION}/linux"
        PROTOBUF_UE4_ZIP                 = "protobuf-ue4-${params.PROTOBUF_UE4_VERSION}-linux.zip"
    }
    stages {
        stage('Cleanup') {
            steps {
                script {
                    if (params.CLEAN_WS) {
                        cleanWs()
                    }
                }
            }
        }

        stage('Download') {
            steps {
                git gitRepo
            }
        }

        stage('Git Engine') {
            steps {
                script {
                    def ue4CleanBuild = !fileExists(ENGINE_LOCAL_DIR)
                    if (ue4CleanBuild) {
                        buildEngine.ue4Checkout(ENGINE_LOCAL_DIR, ENGINE_SYS, params.ENGINE_BRANCH)
                        dir(ENGINE_LOCAL_DIR) {
                            buildEngine.ue4Setup(ENGINE_SYS, '')
                        }
                    }

                    dir(ENGINE_LOCAL_DIR) {
                        def engineVersion = buildEngine.getEngineVersion()
                        echo "Engine version : ${engineVersion}"
                        def engineToolChain = getToolChain(engineVersion)
                        if (engineToolChain == null) {
                            error "Not supported for this engine version : ${engineVersion}"
                        }
                        env.UE4_CLANG_VERSION = engineToolChain.UE4_CLANG_VERSION
                        env.UE4_ZLIB_VERSION = engineToolChain.UE4_ZLIB_VERSION
                    }
                }
            }
        }
        
        stage('Build') {
            steps {
                withCMake('3.12.2') {
                    sh 'rm -rf *.zip'
                    sh './Build_Linux.sh'
                }
            }
        }

        stage('Archive') {
            steps {
                zip archive: true, dir: env.PROTOBUF_UE4_PREFIX, zipFile: env.PROTOBUF_UE4_ZIP
            }
        }
    }
}